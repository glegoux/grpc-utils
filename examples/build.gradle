buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        google()
    }
    dependencies {
        classpath("com.google.protobuf:protobuf-gradle-plugin:${protobufGradlePluginVersion}")
    }
}

plugins {
    id "java"
    id "java-library"
    id "com.google.protobuf"
    id "idea"
    id "application"
    id "distribution"
    id "jacoco"
}


repositories {
    mavenCentral()
}

dependencies {
    // gRPC Utils project
    implementation platform(project(":dependencies"))
    implementation project(':grpc-utils')
    implementation('javax.annotation:javax.annotation-api')
    testImplementation("junit:junit")
    testImplementation('org.assertj:assertj-core')
    testImplementation("junit:junit")
    testImplementation("io.grpc:grpc-testing")
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${protocVersion}"
    }

    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
    }

    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

test {
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
}

application {
    mainClass = "com.glegoux.grpc.examples.hello.HelloServer"
    applicationDefaultJvmArgs = [
            "-javaagent:./jmx-exporter/jmx_prometheus_javaagent-0.16.1.jar=8002:./jmx-exporter/jmx_prometheus_config.yml"
    ]
}

jar {
    manifest {
        attributes(
                'Main-Class': 'com.glegoux.grpc.examples.hello.HelloServer'
        )
    }
}
